#compdef aws-resource-lookup alu

function _aws-resource-lookup() {
    local -a subcommands
    subcommands=(
        'ec2:Lookup EC2 instance by name'
        'ecs:Lookup ECS cluster by name'
        'rds:Lookup RDS instance by name'
        'rds-secret:Lookup RDS master secret and value'
    )

    _arguments -C \
        '1:subcommand:->subcmds' \
        '*:argument:->args'

    case $state in
    subcmds)
        _describe 'subcommand' subcommands
        ;;
    args)
        case $words[2] in
        ec2)
            local names
            names=($(aws ec2 describe-instances \
                --query 'Reservations[].Instances[].Tags[?Key==`Name`].Value' \
                --output text 2>/dev/null))
            _describe 'EC2 instance names' names
            ;;
        ecs)
            local clusters
            clusters=($(aws ecs list-clusters \
                --query 'clusterArns' --output text 2>/dev/null | awk -F/ '{print $NF}'))
            _describe 'ECS clusters' clusters
            ;;
        rds | rds-secret)
            local rds_instances
            rds_instances=($(aws rds describe-db-instances \
                --query 'DBInstances[].DBInstanceIdentifier' \
                --output text 2>/dev/null))
            _describe 'RDS instance names' rds_instances
            ;;
        esac
        ;;
    esac
}
